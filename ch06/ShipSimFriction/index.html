<!DOCTYPE html>
<html>
<head>
	<meta charset="UTF-8">
	<title>Processing.js Animation</title>
	<meta name="description" content="Processing.js Animation Examples. Based on Keith Peters's book. " />
	<meta name="keywords" content="processing, processing.js, flash, animation, false.jp, METAPHOR, metaphor, metaphor inc, metaphor.co.jp, Design, web, web design, toshiyuki nagashima, Reactive, Kinetic, Design, Interface, Interactive, Interaction, Interactive Art, Kinetic Art, Software Art" />
	<meta name="Robots" content="index,follow" />	
	<link rel="shortcut icon" href="" />
	<link rel="home" href="" />
	<link rel="index" href="" />
	<script src="../../global/scripts/lib/jquery-1.8.3.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/lib/processing-1.4.1.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/base.js" type="text/javascript" charset="utf-8"></script>
	<link rel="stylesheet" href="../../global/styles/base.css" type="text/css" />
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shCore.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushJScript.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushProcessing.js"></script>
	<link type="text/css" rel="stylesheet" href="../../global/syntaxhighlighter3/styles/shCoreDefault.css"/>
	<script type="text/javascript">SyntaxHighlighter.all();</script>
</head>
<body>
<header id="globalheader">
	<h1>ch06: ShipSimFriction</h1>
</header>

<div id="main">
	<canvas id="canvas" data-processing-sources="ShipSimFriction.pde"></canvas>
</div>

<div class="sourceContainer">
<script class="brush: processing; toolbar: false;" type="syntaxhighlighter"><![CDATA[
Ship ship;
float vr = 0;
float thrust = 0;
float vx = 0;
float vy = 0;
float friction = 0.97;

void setup() {
  size(960, 540);
  frameRate(30);
  initialize();
}

void initialize() {
  smooth();
  ship = new Ship();
  ship.x = width/2;
  ship.y = height/2;
  ship.setStrokeColor(color(255, 0, 0));
  ship.setFillColor(color(255, 255, 255));
}

void keyPressed() {
  switch(keyCode) {
    case LEFT: 
      vr = -5;
      break;
    case RIGHT: 
      vr = 5;
      break;
    case UP:
      thrust = 0.2;
      ship.gun(true);
      break;
    default:
      break;
  }
}

void keyReleased() {
  vr = 0;
  thrust = 0;
  ship.gun(false);  
}

void draw() {
  background(255);
  
  fill(255, 255, 255);
  stroke(255, 0, 0);
  
  ship.rotation += vr;
  float angle = ship.rotation * PI / 180;
  float ax = cos(angle) * thrust;
  float ay = sin(angle) * thrust;
  vx += ax;
  vy += ay;
  vx *= friction;
  vy *= friction;    
  ship.x += vx;
  ship.y += vy;
  float left = 0;
  float right = width;
  float top = 0;
  float bottom = height;

  if(ship.x - ship.width / 2 > right){
    ship.x = left - ship.width / 2;
  }else if (ship.x + ship.width / 2 < left){
    ship.x = right + ship.width / 2;
  }
      
  if(ship.y - ship.height / 2 > bottom){
    ship.y = top - ship.height / 2;
  }else if (ship.y < top - ship.height / 2){
    ship.y = bottom + ship.height / 2;
  }  
 
  ship.update();
}

////////////////////////////////

class Ship extends Sprite {
  boolean isGun;
  
  Ship() {
    super();
    isGun = false;  
  }
  
  void gun(boolean isGun) {
    this.isGun = isGun;
  }
  
  void draw() {
    if(isGun) {
      beginShape();
      vertex(-7.5, -5);
      vertex(-15, 0);
      vertex(-7.5, 5);
      endShape(CLOSE);
    }      
    beginShape();
    vertex(10, 0);
    vertex(-10, 10);
    vertex(-5, 0);
    vertex(-10, -10);
    vertex(10, 0);
    endShape(CLOSE);
  }
}

////////////////////////////////

class Sprite {
  float x, y;
  float scaleX, scaleY;
  float width, height;
  float rotation;
  boolean visible;
  color strokeColor;
  color fillColor;
  Boolean isStroke;
  Boolean isFill;
  
  Sprite() {
    x = 0.0;
    y = 0.0;
    scaleX = 1.0;
    scaleY = 1.0;
    rotation = 0.0;
    strokeColor = color(0);
    fillColor = color(0);
    isStroke = false;
    isFill = false;
    visible = true;
  }
  
  void update() {
    if(! visible) return;
    pushMatrix();
    translate(x, y);
    scale(scaleX, scaleY);
    rotate(rotation * PI / 180.0);
    if(isStroke) stroke(strokeColor);
    if(isFill) fill(fillColor);
    draw();
    popMatrix();
  }
  
  void setStrokeColor(color strokeColor) {
    this.strokeColor = strokeColor;
    isStroke = true;
  }
  
  void setFillColor(color fillColor) {
    this.fillColor = fillColor;
    isFill = true;
  }
  
  void draw() {}
}
]]></script>
</div>


<div class="readme">
	<div class="readmebox">
	<div class="readme_left">
		<p>Processing.js Animation Examples. Based on Keith Peters's book.  <a href="http://www.amazon.co.jp/ActionScript-3-0-アニメーション-Keith-Peters/dp/4862460496/ref=sr_1_1?ie=UTF8&qid=1365654945&sr=8-1&keywords=actionscript+3.0+animation">Amazon</a></p>
		<ul>
			<li><a href="http://processingjs.org">Processing.js</a> - about Processing.js</li>
			<li><a href="http://www.processing.org">Processing</a> - about Processing</li>
			<li><a href="http://www.bit-101.com/">BIT-101</a> - Keith Peters</li>
			<li><a href="http://www.metaphor.co.jp">METAPHOR</a> - My Company</li>
			<li><a href="http://false.jp">false.jp</a> - My personal project</li>
			<li><a href="http://mutt.jp">MUTT</a> - My personal project</li>
		</ul>
	</div>
	</div>
<script type="text/javascript">

  var _gaq = _gaq || [];
  _gaq.push(['_setAccount', 'UA-40217031-1']);
  _gaq.push(['_trackPageview']);

  (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
  })();

</script>
</body>
</html>