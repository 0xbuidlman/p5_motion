<!DOCTYPE html>
<html>
<head>
	<meta charset="UTF-8">
	<title>Processing.js Animation</title>
	<meta name="description" content="Processing.js Animation Examples. Based on Keith Peters's book. " />
	<meta name="keywords" content="processing, processing.js, flash, animation, false.jp, METAPHOR, metaphor, metaphor inc, metaphor.co.jp, Design, web, web design, toshiyuki nagashima, Reactive, Kinetic, Design, Interface, Interactive, Interaction, Interactive Art, Kinetic Art, Software Art" />
	<meta name="Robots" content="index,follow" />	
	<link rel="shortcut icon" href="" />
	<link rel="home" href="" />
	<link rel="index" href="" />
	<script src="../../global/scripts/lib/jquery-1.8.3.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/lib/processing-1.4.1.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/base.js" type="text/javascript" charset="utf-8"></script>
	<link rel="stylesheet" href="../../global/styles/base.css" type="text/css" />
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shCore.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushJScript.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushProcessing.js"></script>
	<link type="text/css" rel="stylesheet" href="../../global/syntaxhighlighter3/styles/shCoreDefault.css"/>
	<script type="text/javascript">SyntaxHighlighter.all();</script>
</head>
<body>
<header id="globalheader">
	<h1>ch19: SameMass</h1>
</header>

<div id="main">
	<canvas id="canvas" data-processing-sources="SameMass.pde"></canvas>
</div>

<div class="sourceContainer">
<script class="brush: processing; toolbar: false;" type="syntaxhighlighter"><![CDATA[
Ball[] balls;
static final int numBalls = 20;
float bounce = -1.0;

void setup() {
  size(960, 540);
  frameRate(30);
  initialize();
}

void initialize() {
  smooth();
  noStroke();
  
  balls = new Ball[numBalls];
  for(int i=0; i<numBalls; i++) {
    float radius = 30;
    balls[i] = new Ball(radius);
    balls[i].mass = radius;
    balls[i].x = random(width);
    balls[i].y = random(height);
    balls[i].vx = random(10) - 5;
    balls[i].vy = random(10) - 5;
    balls[i].setFillColor(color(255, 0, 0));
  }
  
}

void draw() {
  background(255);

  for(int i=0; i<numBalls; i++){
    Ball ball = balls[i];
    ball.x += ball.vx;
    ball.y += ball.vy;
    checkWalls(ball);
    ball.update();
  }
      
  for(int i = 0; i < numBalls - 1; i++){
    Ball ballA = balls[i];
    for(int j=i+1; j<numBalls; j++){
       Ball ballB = balls[j];
        checkCollision(ballA, ballB);
    }
  }
}

void checkWalls(Ball ball) {
      if(ball.x + ball.radius > width){
        ball.x = width - ball.radius;
        ball.vx *= bounce;
      }else if(ball.x - ball.radius < 0){
        ball.x = ball.radius;
        ball.vx *= bounce;
      }
      
      if(ball.y + ball.radius > height){
        ball.y = height - ball.radius;
        ball.vy *= bounce;
      }else if(ball.y - ball.radius < 0){
        ball.y = ball.radius;
        ball.vy *= bounce;
      }
}

void checkCollision(Ball ball0, Ball ball1) {
      float dx = ball1.x - ball0.x;
      float dy = ball1.y - ball0.y;
      float dist = sqrt(dx*dx + dy*dy);
      
      if(dist < ball0.radius + ball1.radius){
        float angle = atan2(dy, dx);
        float sine = sin(angle);
        float cosine = cos(angle);
        
        Point pos0 = new Point(0, 0);
        Point pos1 = rotate(dx, dy, sine, cosine, true);
        Point vel0 = rotate(ball0.vx, ball0.vy, sine, cosine, true);
        Point vel1 = rotate(ball1.vx, ball1.vy, sine, cosine, true);
    
        float vxTotal = vel0.x - vel1.x;
        vel0.x = ((ball0.mass - ball1.mass) * vel0.x + 2 * ball1.mass * vel1.x) / (ball0.mass + ball1.mass);
        vel1.x = vxTotal + vel0.x;

        float absV = abs(vel0.x) + abs(vel1.x);
        float overlap = (ball0.radius + ball1.radius)  - abs(pos0.x - pos1.x);
        pos0.x += vel0.x / absV * overlap;
        pos1.x += vel1.x / absV * overlap;
        
        Point pos0F = rotate(pos0.x, pos0.y, sine, cosine, false);
        Point pos1F = rotate(pos1.x, pos1.y, sine, cosine, false);

        ball1.x = ball0.x + pos1F.x;
        ball1.y = ball0.y + pos1F.y;
        ball0.x = ball0.x + pos0F.x;
        ball0.y = ball0.y + pos0F.y;
        
        Point vel0F = rotate(vel0.x, vel0.y, sine, cosine, false);
        Point vel1F = rotate(vel1.x, vel1.y, sine, cosine,false);
        ball0.vx = vel0F.x;
        ball0.vy = vel0F.y;
        ball1.vx = vel1F.x;
        ball1.vy = vel1F.y;
      }
}

Point rotate(float x, float y, float sine, float cosine, Boolean reverse) {
  Point result = new Point(0, 0);
  if(reverse){
    result.x = x * cosine + y * sine;
    result.y = y * cosine - x * sine;
  }else{
    result.x = x * cosine - y * sine;
    result.y = y * cosine + x * sine;
  }
  return result;  
}

////////////////////////////////

class Ball extends Sprite {
  float radius;
  float vx, vy;
  float mass = 1;
  
  Ball(float radius){
    super();
    this.radius = radius;
  }
  
  void draw(){
    if(dist(mouseX, mouseY, x, y) < radius) {
      isRollOver = true;
    }else{
      isRollOver = false;
    }
    ellipse(0, 0, radius*2, radius*2);
  }
}

////////////////////////////////

class Sprite extends Object {
  float x, y;
  float scaleX, scaleY;
  float width, height;
  float rotation;
  boolean visible;
  color strokeColor;
  color fillColor;
  Boolean isStroke;
  Boolean isFill;
  Boolean isRollOver;
  Boolean locked;
  float xoff, yoff;
 
  Sprite() {
    super();
    scaleX = 1.0;
    scaleY = 1.0;
    strokeColor = color(0);
    fillColor = color(0);
    isStroke = false;
    isFill = false;
    isRollOver = false;
    locked = false;
    visible = true;
  }
  
  void update() {
    if(! visible) return;
    pushMatrix();
    if(locked){
      x = mouseX - xoff;
      y = mouseY - yoff;
    }  
    translate(x, y);
    scale(scaleX, scaleY);
    rotate(rotation * PI / 180.0);
    if(isStroke) stroke(strokeColor);
    if(isFill) fill(fillColor);
    draw();
    popMatrix();
  }
  
  void setStrokeColor(color strokeColor) {
    this.strokeColor = strokeColor;
    isStroke = true;
  }
  
  void setFillColor(color fillColor) {
    this.fillColor = fillColor;
    isFill = true;
  }
  
  void startDrag() {
    if(isRollOver){
      locked = true;
      xoff = mouseX - x;
      yoff = mouseY - y;
    }
  }
  
  void stopDrag() {
    locked = false;
  }
  
  void draw() {}
}

////////////////////////////////

class Point extends Object {
  float x, y;
  
  Point(float x, float y) {
    super();
    this.x = x;
    this.y = y;
  }
}

////////////////////////////////

class Object {
  Object() {}
}


]]></script>
</div>


<div class="readme">
	<div class="readmebox">
	<div class="readme_left">
		<p>Processing.js Animation Examples. Based on Keith Peters's book.  <a href="http://www.amazon.co.jp/ActionScript-3-0-アニメーション-Keith-Peters/dp/4862460496/ref=sr_1_1?ie=UTF8&qid=1365654945&sr=8-1&keywords=actionscript+3.0+animation">Amazon</a></p>
		<ul>
			<li><a href="http://processingjs.org">Processing.js</a> - about Processing.js</li>
			<li><a href="http://www.processing.org">Processing</a> - about Processing</li>
			<li><a href="http://www.bit-101.com/">BIT-101</a> - Keith Peters</li>
			<li><a href="http://www.metaphor.co.jp">METAPHOR</a> - My Company</li>
			<li><a href="http://false.jp">false.jp</a> - My personal project</li>
			<li><a href="http://mutt.jp">MUTT</a> - My personal project</li>
		</ul>
	</div>
	</div>
<script type="text/javascript">

  var _gaq = _gaq || [];
  _gaq.push(['_setAccount', 'UA-40217031-1']);
  _gaq.push(['_trackPageview']);

  (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
  })();

</script>
</body>
</html>