<!DOCTYPE html>
<html>
<head>
	<meta charset="UTF-8">
	<title>Processing.js Animation</title>
	<meta name="description" content="Processing.js Animation Examples. Based on Keith Peters's book. " />
	<meta name="keywords" content="processing, processing.js, flash, animation, false.jp, METAPHOR, metaphor, metaphor inc, metaphor.co.jp, Design, web, web design, toshiyuki nagashima, Reactive, Kinetic, Design, Interface, Interactive, Interaction, Interactive Art, Kinetic Art, Software Art" />
	<meta name="Robots" content="index,follow" />	
	<link rel="shortcut icon" href="" />
	<link rel="home" href="" />
	<link rel="index" href="" />
	<script src="../../global/scripts/lib/jquery-1.8.3.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/lib/processing-1.4.1.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/base.js" type="text/javascript" charset="utf-8"></script>
	<link rel="stylesheet" href="../../global/styles/base.css" type="text/css" />
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shCore.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushJScript.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushProcessing.js"></script>
	<link type="text/css" rel="stylesheet" href="../../global/syntaxhighlighter3/styles/shCoreDefault.css"/>
	<script type="text/javascript">SyntaxHighlighter.all();</script>
</head>
<body>
<header id="globalheader">
	<h1>ch15: Tree1</h1>
</header>

<div id="main">
	<canvas id="canvas" data-processing-sources="Tree1.pde"></canvas>
</div>

<div class="sourceContainer">
<script class="brush: processing; toolbar: false;" type="syntaxhighlighter"><![CDATA[
Tree[] trees;
static final int numTrees = 100;
float fl = 250;
float vpX = 0;
float vpY = 0;
float floor = 50;
float vz = 0;
float friction = 0.98;

void setup() {
  size(960, 540);
  frameRate(30);
  initialize();
}

void initialize() {
  smooth();
  vpX = width/2;
  vpY = height/2;
  trees = new Tree[numTrees];
  for(int i=0; i<numTrees; i++) {
    trees[i] = new Tree();
    trees[i].xpos = random(2000) - 1000;
    trees[i].ypos = floor;
    trees[i].zpos = random(10000);
    trees[i].setFillColor(color(255, 0, 0));   
  }
}

void draw() {
  background(0);
  for(int i=0; i<numTrees; i++) {
    Tree tree = trees[i];
    move(tree);
  }
  vz *= friction;
  sortZ();
}

void move(Tree tree) {
  tree.zpos += vz;
  if(tree.zpos < -fl) {
    tree.zpos += 10000;
  }
  if(tree.zpos > 10000 - fl) {
    tree.zpos -= 10000;
  }
  float scale = fl / (fl + tree.zpos);
  tree.scaleX = tree.scaleY = scale;
  tree.x = vpX + tree.xpos * scale;
  tree.y = vpY + tree.ypos * scale;
  tree.setStrokeColor(color(255, 255, 255));
  tree.setStrokeColor(color(255, 255, 255, 200 + 55*scale));
}

void sortZ() {
  for(int i=0; i<numTrees - 1; i++) {
    for(int j=numTrees-1; j>i; j--) {
      if(trees[j].zpos > trees[j-1].zpos) {
        Tree t = trees[j];
        trees[j] = trees[j-1];
        trees[j-1] = t;
      }
    }
  }

  for(int i=0; i<numTrees; i++) {
     Tree tree = trees[i];
     tree.update();
  }
}

void keyPressed() {
  switch(keyCode) {
    case UP:
      vz -= 1;
      break;
    case DOWN:
      vz += 1;
      break;
    default:
      break;
  }
}

////////////////////////////////

class Segment extends Sprite {
  float segmentWidth, segmentHeight;
  float vx, vy;
  
  Segment(float segmentWidth, float segmentHeight) {
    super();
    this.segmentWidth = segmentWidth;
    this.segmentHeight = segmentHeight;
  }
  
  void draw() {
    rect(-segmentHeight / 2,  -segmentHeight / 2, segmentWidth + segmentHeight, segmentHeight, segmentHeight, segmentHeight, segmentHeight, segmentHeight);
    ellipse(0, 0, 2, 2);
    ellipse(segmentWidth, 0, 2, 2);
  }
  
  Point getPin() {
    float angle = rotation * PI / 180;
    float xPos = x + cos(angle) * segmentWidth;
    float yPos = y + sin(angle) * segmentWidth;
    return new Point(xPos, yPos);  
  }
}

////////////////////////////////

class Tree extends Sprite {
  float xpos = 0;
  float ypos = 0;
  float zpos = 0;
  float x0, y0, x1, y1, x2, y2, x3, y3, x4, y4;
  
  Tree(){
    super();
    x0 = 0;
    y0 = -140 - random(20);
    x1 = 0;
    y1 = -30 - random(30);
    x2 = random(80) - 40;
    y2 = -100 - random(40);
    x3 = 0;
    y3 = -60 - random(40);
    x4 = random(60) - 30;
    y4 = -110 - random(20);
  }
  
  void draw(){
    line(0, 0, x0, y0);
    line(x1, y1, x2, y2);
    line(x3, y3, x4, y4);
  }
}

////////////////////////////////

class Sprite extends Object {
  float x, y;
  float scaleX, scaleY;
  float width, height;
  float rotation;
  boolean visible;
  color strokeColor;
  color fillColor;
  Boolean isStroke;
  Boolean isFill;
  Boolean isRollOver;
  Boolean locked;
  float xoff, yoff;
 
  Sprite() {
    super();
    scaleX = 1.0;
    scaleY = 1.0;
    strokeColor = color(0);
    fillColor = color(0);
    isStroke = false;
    isFill = false;
    isRollOver = false;
    locked = false;
    visible = true;
  }
  
  void update() {
    if(! visible) return;
    pushMatrix();
    if(locked){
      x = mouseX - xoff;
      y = mouseY - yoff;
    }  
    translate(x, y);
    scale(scaleX, scaleY);
    rotate(rotation * PI / 180.0);
    if(isStroke) stroke(strokeColor);
    if(isFill) fill(fillColor);
    draw();
    popMatrix();
  }
  
  void setStrokeColor(color strokeColor) {
    this.strokeColor = strokeColor;
    isStroke = true;
  }
  
  void setFillColor(color fillColor) {
    this.fillColor = fillColor;
    isFill = true;
  }
  
  void startDrag() {
    if(isRollOver){
      locked = true;
      xoff = mouseX - x;
      yoff = mouseY - y;
    }
  }
  
  void stopDrag() {
    locked = false;
  }
  
  void draw() {}
}

////////////////////////////////

class Point extends Object {
  float x, y;
  
  Point(float x, float y) {
    super();
    this.x = x;
    this.y = y;
  }
}

////////////////////////////////

class Object {
  Object() {}
}


]]></script>
</div>


<div class="readme">
	<div class="readmebox">
	<div class="readme_left">
		<p>Processing.js Animation Examples. Based on Keith Peters's book.  <a href="http://www.amazon.co.jp/ActionScript-3-0-アニメーション-Keith-Peters/dp/4862460496/ref=sr_1_1?ie=UTF8&qid=1365654945&sr=8-1&keywords=actionscript+3.0+animation">Amazon</a></p>
		<ul>
			<li><a href="http://processingjs.org">Processing.js</a> - about Processing.js</li>
			<li><a href="http://www.processing.org">Processing</a> - about Processing</li>
			<li><a href="http://www.bit-101.com/">BIT-101</a> - Keith Peters</li>
			<li><a href="http://www.metaphor.co.jp">METAPHOR</a> - My Company</li>
			<li><a href="http://false.jp">false.jp</a> - My personal project</li>
			<li><a href="http://mutt.jp">MUTT</a> - My personal project</li>
		</ul>
	</div>
	</div>
<script type="text/javascript">

  var _gaq = _gaq || [];
  _gaq.push(['_setAccount', 'UA-40217031-1']);
  _gaq.push(['_trackPageview']);

  (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
  })();

</script>
</body>
</html>