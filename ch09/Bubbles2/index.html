<!DOCTYPE html>
<html>
<head>
	<meta charset="UTF-8">
	<title>Processing.js Animation</title>
	<meta name="description" content="Processing.js Animation Examples. Based on Keith Peters's book. " />
	<meta name="keywords" content="processing, processing.js, flash, animation, false.jp, METAPHOR, metaphor, metaphor inc, metaphor.co.jp, Design, web, web design, toshiyuki nagashima, Reactive, Kinetic, Design, Interface, Interactive, Interaction, Interactive Art, Kinetic Art, Software Art" />
	<meta name="Robots" content="index,follow" />	
	<link rel="shortcut icon" href="" />
	<link rel="home" href="" />
	<link rel="index" href="" />
	<script src="../../global/scripts/lib/jquery-1.8.3.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/lib/processing-1.4.1.min.js" type="text/javascript" charset="utf-8"></script>
	<script src="../../global/scripts/base.js" type="text/javascript" charset="utf-8"></script>
	<link rel="stylesheet" href="../../global/styles/base.css" type="text/css" />
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shCore.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushJScript.js"></script>
	<script type="text/javascript" src="../../global/syntaxhighlighter3/scripts/shBrushProcessing.js"></script>
	<link type="text/css" rel="stylesheet" href="../../global/syntaxhighlighter3/styles/shCoreDefault.css"/>
	<script type="text/javascript">SyntaxHighlighter.all();</script>
</head>
<body>
<header id="globalheader">
	<h1>ch09: Bubbles2</h1>
</header>

<div id="main">
	<canvas id="canvas" data-processing-sources="Bubbles2.pde"></canvas>
</div>

<div class="sourceContainer">
<script class="brush: processing; toolbar: false;" type="syntaxhighlighter"><![CDATA[
Ball[] balls;
static final int numBalls = 30;
float bounce = -0.5;
float spring = 0.05;
float gravity = 0.1;

void setup() {
  size(960, 540);
  frameRate(30);
  initialize();
}

void initialize() {
  smooth();
  noStroke();

  balls = new Ball[numBalls];
  for(int i=0; i<numBalls; i++) {
    float radius = random(30) + 20;
    balls[i] = new Ball(radius);
    balls[i].x = random(width);
    balls[i].y = random(height);
    balls[i].vx = random(6) - 3;
    balls[i].vy = random(6) - 3;
    balls[i].setFillColor(color(random(255), random(255), random(255)));
  }
}

void draw() {
  background(255);

  for(int i=0; i<numBalls-1; i++){
    Ball ball0 = balls[i];
    for(int j=i+1; j<numBalls; j++){
      Ball ball1 = balls[j];
      float dx = ball1.x - ball0.x;
      float dy = ball1.y - ball0.y;
      float dist = sqrt(dx * dx + dy * dy);
      float minDist = ball0.radius + ball1.radius;
      if(dist < minDist){
        float angle = atan2(dy, dx);
        float tx = ball0.x + dx / dist * minDist;
        float ty = ball0.y + dy / dist * minDist;
        float ax = (tx - ball1.x) * spring;
        float ay = (ty - ball1.y) * spring;
        ball0.vx -= ax;
        ball0.vy -= ay;
        ball1.vx += ax;
        ball1.vy += ay;
      }
    }
  }
      
  for(int i=0; i<numBalls; i++) {
    Ball ball = balls[i];
    move(ball);
    ball.update();
  }
}

void move(Ball ball) {
  ball.vy += gravity;
  ball.x += ball.vx;
  ball.y += ball.vy;
      
  if(ball.x + ball.radius > width){
    ball.x = width - ball.radius;
    ball.vx *= bounce;
  }else if(ball.x - ball.radius < 0){
    ball.x = ball.radius;
    ball.vx *= bounce;
  }
      
  if(ball.y + ball.radius > height){
    ball.y = height - ball.radius;
    ball.vy *= bounce;
  }else if(ball.y - ball.radius < 0){
    ball.y = ball.radius;
    ball.vy *= bounce;
  }
}

////////////////////////////////

class Ball extends Sprite {
  float radius;
  float vx, vy;
  float mass = 1;
  
  Ball(float radius){
    super();
    this.radius = radius;
  }
  
  void draw(){
    if(dist(mouseX, mouseY, x, y) < radius) {
      isRollOver = true;
    }else{
      isRollOver = false;
    }
    ellipse(0, 0, radius*2, radius*2);
  }
}

////////////////////////////////

class Sprite extends Object {
  float x, y;
  float scaleX, scaleY;
  float width, height;
  float rotation;
  boolean visible;
  color strokeColor;
  color fillColor;
  Boolean isStroke;
  Boolean isFill;
  Boolean isRollOver;
  Boolean locked;
  float xoff, yoff;
 
  Sprite() {
    super();
    scaleX = 1.0;
    scaleY = 1.0;
    strokeColor = color(0);
    fillColor = color(0);
    isStroke = false;
    isFill = false;
    isRollOver = false;
    locked = false;
    visible = true;
  }
  
  void update() {
    if(! visible) return;
    pushMatrix();
    if(locked){
      x = mouseX - xoff;
      y = mouseY - yoff;
    }  
    translate(x, y);
    scale(scaleX, scaleY);
    rotate(rotation * PI / 180.0);
    if(isStroke) stroke(strokeColor);
    if(isFill) fill(fillColor);
    draw();
    popMatrix();
  }
  
  void setStrokeColor(color strokeColor) {
    this.strokeColor = strokeColor;
    isStroke = true;
  }
  
  void setFillColor(color fillColor) {
    this.fillColor = fillColor;
    isFill = true;
  }
  
  void startDrag() {
    if(isRollOver){
      locked = true;
      xoff = mouseX - x;
      yoff = mouseY - y;
    }
  }
  
  void stopDrag() {
    locked = false;
  }
  
  void draw() {}
}

////////////////////////////////

class Point extends Object {
  float x, y;
  
  Point(float x, float y) {
    super();
    this.x = x;
    this.y = y;
  }
}

////////////////////////////////

class Object {
  Object() {}
}


]]></script>
</div>


<div class="readme">
	<div class="readmebox">
	<div class="readme_left">
		<p>Processing.js Animation Examples. Based on Keith Peters's book.  <a href="http://www.amazon.co.jp/ActionScript-3-0-アニメーション-Keith-Peters/dp/4862460496/ref=sr_1_1?ie=UTF8&qid=1365654945&sr=8-1&keywords=actionscript+3.0+animation">Amazon</a></p>
		<ul>
			<li><a href="http://processingjs.org">Processing.js</a> - about Processing.js</li>
			<li><a href="http://www.processing.org">Processing</a> - about Processing</li>
			<li><a href="http://www.bit-101.com/">BIT-101</a> - Keith Peters</li>
			<li><a href="http://www.metaphor.co.jp">METAPHOR</a> - My Company</li>
			<li><a href="http://false.jp">false.jp</a> - My personal project</li>
			<li><a href="http://mutt.jp">MUTT</a> - My personal project</li>
		</ul>
	</div>
	</div>
<script type="text/javascript">

  var _gaq = _gaq || [];
  _gaq.push(['_setAccount', 'UA-40217031-1']);
  _gaq.push(['_trackPageview']);

  (function() {
    var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
    ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
  })();

</script>
</body>
</html>